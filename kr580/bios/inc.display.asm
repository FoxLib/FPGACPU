
cursor_xy:      defb    0, 0            ; Текущее положение курсора
cursor_attr:    defb    0               ; Текущий цветовой атрибут

; ----------------------------------------------------------------------
; Процедура очистки экрана, в регистре A атрибут
; ----------------------------------------------------------------------

cls:        ld      hl, $4000
            ld      b, $00
            ld      c, a
            ld      (cursor_attr), a
p1m1:       ld      (hl), b
            inc     l
            jr      nz, p1m1
            inc     h
            ld      a, h
            cp      $5B
            ret     z
            cp      $58
            jr      nz, p1m1
            ld      b, c
            jr      p1m1

; ----------------------------------------------------------------------
; Печать символа A, позиция B=Y, C=X
; ----------------------------------------------------------------------

prn:        push    hl
            push    de
            push    bc

            ; Вычисление позиции символа в таблице символов
            sub     a, $20
            ld      h, 0
            ld      l, a
            add     hl, hl
            add     hl, hl
            add     hl, hl
            ld      de, fonts
            add     hl, de
            ex      de, hl

            ; Расчет позиции HL
            ld      a, c
            and     0x1F
            ld      l, a    ; L = X & 31
            ld      a, b
            and     0x07    ; Нужно ограничить 3 битами
            rrca            ; Легче дойти с [0..2] до позиции [5..7]
            rrca            ; Если вращать направо
            rrca            ; ... три раза
            or      l       ; Объединив с 0..4 уже готовыми ранее
            ld      l, a    ; Загрузить новый результат в L
            ld      a, b    ; Т.к. Y[3..5] уже на месте
            and     0x18    ; Его двигать даже не надо
            or      0x40    ; Ставим видеоадрес $4000
            ld      h, a    ; И загружаем результат

            ; Рисование
            ld      b, 8
p2m1:       ld      a, (de)
            ld      (hl), a
            inc     de
            inc     h
            djnz    p2m1

            pop     bc
            pop     de
            pop     hl
            ret

; ----------------------------------------------------------------------
; Вычисление 32*Y + X -> HL, AF затронуто
; ----------------------------------------------------------------------

attrpl:     push    de
            ld      hl, (cursor_xy)
            ld      e, l
            ld      l, h
            ld      d, 0
            ld      h, 0
            add     hl, hl
            add     hl, hl
            add     hl, hl
            add     hl, hl
            add     hl, hl      ; 32*Y
            add     hl, de      ; HL = 32*Y + X
            ld      a, h
            add     $58
            ld      h, a
            pop     de
            ret

; ----------------------------------------------------------------------
; Ставится атрибут в позицию курсора
; ----------------------------------------------------------------------

setat:      push    af
            push    hl
            call    attrpl
            ld      a, (cursor_attr)
            ld      (hl), a
            pop     hl
            pop     af
            ret

; ----------------------------------------------------------------------
; Печать символа A в режиме телетайпа с прокруткой вверх
; ----------------------------------------------------------------------

prnc:       push    bc
            push    de
            push    hl

            ; Текущая позиция курсора
            ld      hl, (cursor_xy) ; Текущий курсор -> BC
            ld      b, h
            ld      c, l
            cp      13              ; ENTER?
            jr      z, p3m2

            call    setat           ; Установка атрибута
            call    prn             ; Печать символа

            inc     l
            ld      a, l
            cp      $20
            jr      nz, p3m1        ; Достиг правого края
p3m2:       ld      l, $00
            inc     h
            ld      a, h
            cp      $18             ; Достиг нижней границы
            jr      nz, p3m1

                ; @todo scroll up

p3m1:       ld      (cursor_xy), hl
            pop     hl
            pop     de
            pop     bc
            ret

; ----------------------------------------------------------------------
; Печать строки из DE в режиме телетайпа
; ----------------------------------------------------------------------

print:      ld      a, (de)
            inc     de
            and     a
            ret     z
            call    prnc
            jr      print

; ----------------------------------------------------------------------
; Обработчик прерывания RST #10
; ----------------------------------------------------------------------
; $00 Положение курсора Y,X => HL
; $01 Установить курсор HL => Y,X
; $02 CLS(B=color)
; $03 Печать строки DE
; $04 Конвертация числа DE -> строка DE
; $05 Чтение сектора HL:DE в  BC
; $06 Запись сектора HL:DE из BC
; $07 16-битное деление DE на BC, DE-рез-т, HL-остаток
; ----------------------------------------------------------------------

rst10:      cp      $08
            jr      nc, rst10n1         ; Просмотреть [08..0F]
            and     a
            jr      z, r10_getxy        ; 00 Get cursor Y,X
            dec     a
            jr      z, r10_setxy        ; 01 Set cursor X
            dec     a
            jr      z, r10_cls          ; 02 CLS, B-color
            dec     a
            jr      z, r10_print        ; 03 Печать из DE
            dec     a
            jr      z, r10_itoa         ; 04 char* itoa(int)
            dec     a
            jr      z, r10_read         ; 05 READ
            dec     a
            jr      z, r10_write        ; 06 WRITE
            call    div16u              ; 07 DIVIDE
rst10n1:    sub     $08
            ret

; Запуск процедур API
r10_getxy:  ld      hl, (cursor_xy)
            ret
r10_setxy:  ld      (cursor_xy), hl
            ret
r10_cls:    ld      a, b
            call    cls
            ret
r10_print:  call    print
            ret
r10_itoa:   call    itoa
            ret
r10_read:   call    read
            ret
r10_write:  call    write
            ret

; ШРИФТЫ
fonts:      incbin  "font.fnt"

; Псевдографика
defb        $00, $00, $ff, $00, $ff, $00, $00, $00 ; $80 =
defb        $28, $28, $28, $28, $28, $28, $28, $28 ; $81 |
defb        $00, $00, $3f, $20, $2f, $28, $28, $28 ; $82 |-
defb        $00, $00, $f8, $08, $e8, $28, $28, $28 ; $83 -|
defb        $28, $28, $2f, $20, $3f, $00, $00, $00 ; $84 |-
defb        $28, $28, $e8, $08, $f8, $00, $00, $00 ; $85 -|

; 0010 1000
; 0010 1000
; 1110 1000
; 0000 1000

; 1111 1000
; 0000 0000
; 0000 0000
; 0000 0000
