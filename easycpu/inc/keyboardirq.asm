; ----------------------------------------------------------------------
; Процедура-обработчик IRQ Keyboard AT&T сканкодов
; RETURN: ACC будет код клавиши и статус нажатия (старший байт)
; AFFECT: r0, flags
; ----------------------------------------------------------------------

keyboardirq:

        lda     [$FFA0]         ; PS2_KEYB_CODE
        clh
        ldi     r0, $00F0
        sub     r0
        jmp z, .unpress         ; Сканкод = 0xF0 -> отметка "Отжата"
        add     r0              ; Вернуть значение ACC
        ldi     r0, .kbtable
        add     r0
        sta     r0
        lda     [r0]            ; ACC = kbtable[ACC]
        clh
        sta     r0
        lda     [.kbpress]
        ora     r0
        sta     r0              ; r0 = ACC | [.kbpress]
        lda     $0000
        sta     [.kbpress]      ; Очистить .kbpress
        lda     r0
        sta     [.symbol]
        ret

.unpress: ; Закрепить за следующей клавишей, что она отжата

        lda     $FF00
        sta     [.kbpress]
        lda     $0000
        ret

.kbpress: dw 0
.symbol:  dw 0

.kbtable:
;        0    1    2    3    4    5    6    7    8    9    A    B    C    D    E    F
db       0 , ' ', ' ', ' ', ' ', ' ', ' ', ' ', ' ', ' ', ' ', ' ', ' ', ' ', '~', ' ' ; 0x
db      ' ', ' ', ' ', ' ', ' ', 'Q', '1', ' ', ' ', ' ', 'Z', 'S', 'A', 'W', '2', ' ' ; 1x
db      ' ', 'C', 'X', 'D', 'E', '4', '3', ' ', ' ', ' ', 'V', 'F', 'T', 'R', '5', ' ' ; 2x
db      ' ', 'N', 'B', 'H', 'G', 'Y', '6', ' ', ' ', ' ', 'M', 'J', 'U', '7', '8', ' ' ; 3x
db      ' ', ',', 'K', 'I', 'O', '0', '9', ' ', ' ', '.', '/', 'L', ';', 'P', '-', ' ' ; 4x
db      ' ', ' ',0x27, ' ', '[', '=', ' ', ' ', ' ', ' ', ' ', ']', ' ',0x5C, ' ', ' ' ; 5x
db      ' ', ' ', ' ', ' ', ' ', ' ', ' ', ' ', ' ', ' ', ' ', ' ', ' ', ' ', ' ', ' ' ; 6x
db      ' ', ' ', ' ', ' ', ' ', ' ',0x01, ' ', ' ', ' ', ' ', ' ', ' ', ' ', ' ', ' ' ; 7x

; ----------------------------------------------------------------------
; Получение последнего нажатого символа --> ACC: ожидать символ
; ----------------------------------------------------------------------

getch:  push    r0
@@:     lda     [keyboardirq.symbol]
        sta     r0
        and     r0
        bra z,  @b
        ; -- тест на shift up/down
        lda     $FF00
        and     r0
        bra nz, @b                  ; Клавиша имеет код "отпущено"
        lda     $0000
        sta     [keyboardirq.symbol]
        lda     r0
        pop     r0
        ret
